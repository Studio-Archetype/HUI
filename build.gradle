plugins {
    id 'java'
    id "io.freefair.lombok" version "6.3.0"
    id "de.undercouch.download" version "5.0.1"
    id "com.github.johnrengelman.shadow" version "7.1.2"
}

// ADD YOURSELF AS A NEW LINE IF YOU WANT YOUR OWN BUILD TASK GENERATED
// ======================== WINDOWS =============================
registerCustomOutputTask('CrazyDev22', 'C://Users/Julian/Desktop/server/plugins')
// ========================== UNIX ==============================
registerCustomOutputTaskUnix('CrazyDev22LT', '/home/julian/Desktop/server/plugins')
// ==============================================================

group 'studio.archetype.hologui2'
version project.version

file(jar.archiveFile.get().getAsFile().getParentFile().getParentFile().getParentFile().getAbsolutePath() + '/build/resources/main/plugin.yml').delete()

compileJava {
    options.encoding = 'UTF-8'
}

shadowJar {
    archiveClassifier = null
    dependencies {
        exclude(dependency("net.kyori:"))
        exclude(dependency("com.google.code.gson:"))
        exclude(dependency("com.google.guava:"))
        exclude(dependency("it.unimi.dsi:"))
        exclude(dependency("org.apache.commons:"))
        exclude(dependency("org.slf4f:"))
    }

    minimize()

    relocate("com.github.retrooper.packetevents", "${libs}.packetevents.api")
    relocate("io.github.retrooper.packetevents", "${libs}.packetevents.impl")
    relocate("co.aikar.commands", "${libs}.acf")
    relocate("co.aikar.locales", "${libs}.locales")
    relocate("com.mojang.serialization", "${libs}.serialization")
}

repositories {
    mavenCentral()
    maven { url "https://libraries.minecraft.net/"}
    maven { url "https://hub.spigotmc.org/nexus/content/repositories/snapshots/" }
    maven { url "https://repo.extendedclip.com/releases/" }
    maven { url "https://repo.codemc.io/repository/maven-releases/" }
    maven { url "https://repo.aikar.co/content/groups/aikar/" }
}

dependencies {
    compileOnly "org.projectlombok:lombok:$lombokVersion"
    annotationProcessor "org.projectlombok:lombok:$lombokVersion"
    compileOnly "org.spigotmc:spigot-api:$spigotJarVersion"
    compileOnly "me.clip:placeholderapi:$placeholderVersion"

    //Shaded
    implementation "com.github.retrooper:packetevents-spigot:$packeteventsVersion"
    implementation "co.aikar:acf-paper:$acfVersion"

    //Provided
    implementation "com.mojang:datafixerupper:$datafixerupperVersion"
    compileOnly "commons-io:commons-io:$commonsIoVersion"

    //Dynamically loaded
    compileOnly "io.undertow:undertow-core:$webServerVersion"
    compileOnly "com.github.zafarkhaja:java-semver:$semVerVersion"
}

processResources {
    filesMatching('**/plugin.yml') {
        expand(
                'name': project.name,
                'version': version,
                'main': main,
                'apiVersion': apiVersion,
                'webServerVersion': webServerVersion,
                'semVerVersion': semVerVersion
        )
    }
}

build.dependsOn(shadowJar)

// IDE Server stuff
def registerCustomOutputTask(name, path) {
    if (!System.properties['os.name'].toLowerCase().contains('windows')) {
        return;
    }
    AbstractCopyTask

    tasks.register('build' + name, Copy) {
        group('dev')
        outputs.upToDateWhen { true }
        dependsOn(build)
        from(new File(buildDir, "libs/holoui-" + version + ".jar"))
        into(file(path))
        rename { String fileName ->
            fileName.replace("holoui-" + version + ".jar", "Holoui.jar")
        }
    }

    tasks.register('downloadBile' + name, Download) {
        group "server"
        src 'https://github.com/VolmitSoftware/BileTools/releases/download/2/BileTools-2.jar'
        dest file(path)
        dest file(path)
    }
}

def registerCustomOutputTaskUnix(name, path) {
    if (System.properties['os.name'].toLowerCase().contains('windows')) {
        return;
    }

    tasks.register('build' + name, Copy) {
        group('dev')
        outputs.upToDateWhen { true }
        dependsOn(build)
        from(new File(buildDir, "libs/holoui-" + version + ".jar"))
        into(file(path))
        rename { String fileName ->
            fileName.replace("holoui-" + version + ".jar", "Holoui.jar")
        }
    }

    tasks.register('downloadBile' + name, Download) {
        group "server"
        src 'https://github.com/VolmitSoftware/BileTools/releases/download/2/BileTools-2.jar'
        dest file(path)
        dest file(path)
    }
}
